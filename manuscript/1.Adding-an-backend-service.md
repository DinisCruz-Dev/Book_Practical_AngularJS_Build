### Adding Backend Service

First step is to add a backend service (called **Data-Project**) which will provide data about the current projects (in this case metadata about the project and its files).

Here is the test

```coffee
Data_Project = require '../../src/backend/Data-Project'

describe 'backend | Data-Project', ->
  data_Project = null

  beforeEach ->
    data_Project = new Data_Project()

  it 'constructor',->
    using data_Project, ->
      @.constructor.name.assert_Is 'Data_Project'
      @.data_Path.assert_Contains 'data'
                 .assert_Folder_Exists()

  it 'project_Files', ->
    using data_Project, ->
      assert_Is_Null @.project_Files('aa')
      console.log @.project_Files()

  it 'projects', ->
    using data_Project, ->
      @.projects()._keys().assert_Contains('demo', 'appsec')

  it 'projects_Keys', ->
    using data_Project, ->
      @.projects_Keys().assert_Contains('demo', 'appsec')      
```

Here is the code

```coffee

class Data_Project
  constructor: ()->
    @.data_Path       = __dirname.path_Combine('../../data')
    @.config_File     = "maturity-model.json"
    @.default_Project = 'demo'

  project_Files: (project_Key)=>
    key = project_Key ||  @.default_Project           # todo: refactor to make it clear
    return using (@.projects()[key]),->
      project_Path = @.path
      if @.path
        values = []
        for file in project_Path.files_Recursive()
          if file.file_Extension() in ['.json', '.coffee']
            if file.not_Contains 'maturity-model'
              values.push file
        return values
      return null

  # returns a list of current projects (which are defined by a folder containing an maturity-model.json )
  projects: ()=>
    projects = {}
    for folder in @.data_Path.folders_Recursive()
      config_File = folder.path_Combine @.config_File
      if config_File.file_Exists()
        data = config_File.load_Json()
        if data and data.key
          projects[data.key] =
            path: folder
            data: data    
    projects

  projects_Keys: ()=>            
    @.projects()._keys()


module.exports = Data_Project
```

Here is what the development UI looks like (with wallaby confirming 100% code coverage)


![image](https://cloud.githubusercontent.com/assets/656739/16160967/f352a33c-34c2-11e6-8b47-9bd88fb9bbae.png)
